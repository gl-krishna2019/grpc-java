plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.17'
    id 'idea'
}

group 'com.github.gl-krishna2019.grpc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.43.1'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

dependencies {
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.43.1'
    implementation 'io.grpc:grpc-protobuf:1.43.1'
    implementation 'io.grpc:grpc-stub:1.43.1'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
}

// if you have source imports issues, add the below
sourceSets.main.java.srcDir new File(buildDir, 'generated/source')
idea {
    module {
        // Marks the already(!) added srcDir as "generated"
        generatedSourceDirs += file('build/generated/source')
    }
}
